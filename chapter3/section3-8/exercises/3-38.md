> Consider the following source code, where M and N are constants declared with
> `#define`:
> ```C
> long P[M][N];
> long Q[N][M];
> long sum_element(long i, long j) {
>     return P[i][j] + Q[j][i];
> }
> ```
> In compiling this program, GCC generates the following assembly code:
> ```Assembly
> # long sum_element(long i, long j)
> # i in %rdi, j in %rsi
> 1   sum_element:
> 2       leaq    0(,%rdi,8), %rdx
> 3       subq    %rdi, %rdx
> 4       addq    %rsi, %rdx
> 5       leaq    (%rsi,%rsi,4), %rax
> 6       addq    %rax, %rdi
> 7       movq    Q(,%rdi,8), %rax
> 8       addq    P(,%rdx,8), %rax
> 9       ret
> ```
> Use your reverse engineering skills to determine the values of M and N based
> on this assembly code.

```Assembly
1   sum_element:
2       leaq    0(,%rdi,8), %rdx        # rdx = 8i
3       subq    %rdi, %rdx              # rdx = 7i
4       addq    %rsi, %rdx              # rdx = 7i + j
5       leaq    (%rsi,%rsi,4), %rax     # rax = 5j
6       addq    %rax, %rdi              # rdi = i + 5j
7       movq    Q(,%rdi,8), %rax        # rax = M[Q + 8(i + 5j)]
8       addq    P(,%rdx,8), %rax        # rax = M[P + 8(7i + j)] + M[Q + 8(i + 5j)]
9       ret
```

On line 7 we see that when accessing Q, each increment in `j` counts as 5
increments in `i`. Looking at the definition of Q, we have `M = 5`.
Repeating the analysis for line 8, we get `N = 7`.
