> Consider the following source code, where `NR` and `NC` are macro expressions
> declared with `#define` that compute the dimensions of array `A` in terms of
> parameter `n`. This code computes the sum of the elements of column `j` of
> the array.
> 
> ```C
> 1  long sum_col(long n, long A[NR(n)][NC(n)], long j) {
> 2      long i;
> 3      long result = 0;
> 4      for (i = 0; i < NR(n); i++)
> 5          result += A[i][j];
> 6      return result;
> 7  }
> ```
> 
> In compiling this program, GCC generates the following assembly code:
> 
> ```Assembly
>    long sum_col(long n, long A[NR(n)][NC(n)], long j)
>    n in %rdi, A in %rsi, j in %rdx
> 1  sum_col:
> 2      leaq 1(,%rdi,4), %r8
> 3      leaq (%rdi,%rdi,2), %rax
> 4      movq %rax, %rdi
> 5      testq %rax, %rax
> 6      jle .L4
> 7      salq $3, %r8
> 8      leaq (%rsi,%rdx,8), %rcx
> 9      movl $0, %eax
> 10     movl $0, %edx
> 11 .L3:
> 12     addq (%rcx), %rax
> 13     addq $1, %rdx
> 14     addq %r8, %rcx
> 15     cmpq %rdi, %rdx
> 16     jne .L3
> 17     rep; ret
> 18 .L4:
> 19     movl $0, %eax
> 20     ret
> ```
> 
> Use your reverse engineering skills to determine the definitions of `NR` and
> `NC`.

```Assembly
   long sum_col(long n, long A[NR(n)][NC(n)], long j)
   n in %rdi, A in %rsi, j in %rdx
1  sum_col:
2      leaq 1(,%rdi,4), %r8         r8 = 4n + 1
3      leaq (%rdi,%rdi,2), %rax     rax = 3n
4      movq %rax, %rdi              rdi = 3n
5      testq %rax, %rax             if 3n <= 0
6      jle .L4                          goto .L4
7      salq $3, %r8                 r8 = (4n + 1) << 3 = 8(4n + 1)
8      leaq (%rsi,%rdx,8), %rcx     rcx = (8j + A)
9      movl $0, %eax                eax = 0
10     movl $0, %edx                edx = 0
11 .L3:
12     addq (%rcx), %rax            rax += *rcx
13     addq $1, %rdx                rdx++
14     addq %r8, %rcx               rcx += 8(4n + 1)
15     cmpq %rdi, %rdx              if rdx != 3n
16     jne .L3                          goto .L3
17     rep; ret
18 .L4:
19     movl $0, %eax                return 0
20     ret
```

```C
1  long sum_col(long n, long A[NR(n)][NC(n)], long j) {
2      long i;
3      long result = 0;
4      for (i = 0; i < NR(n); i++)
5          result += A[i][j];
6      return result;
7  }
```

We can see on lines 5 and 6 of the assemply code that the loop condition is
`3n <= 0`. Even though the access to `A[i][j]` has been optimized to depend
directly on `n` instead of `i`, `i` is still used to do bounds checking. On
lines 5-6 and 15-16 we see that `i` is compared to `3n`. That must be the
definition of NR(n).
```
NR(n) = 3n
```

The initial value of `i` is 0. This means that `&A[i][j]` is, in the first
iteration, `&A[0][j]`. We can easily see on line 12 of the assembly code that,
given that result is accumulated in `rax`, `rcx` must hold the current pointer
to the appropriate `&A[i][j]` position. On line 14 we see that rcx is
incremented by `8(4n + 1)` on each loop. This must have the same effect as
incrementing `i` by one and recalculating `&A[i][j]`.
Knowing that `&A[i][j] = A + 8(NC(n) i + j)`, then
```
&A[i+1][j] = A + 8(NC(n) (i + 1) + j)
           = A + 8(NC(n) i + j) + 8 NC(n)
           = &A[i][j] + 8 NC(n)
```
Given that the increased is expressed in two ways which must be equivalent, we
can derive the definition of `NC(n)`
```
8 NC(n) = 8(4n + 1)
NC(n) = 4n + 1
```
