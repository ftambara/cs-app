> For the C code
> ```C
> long dw_loop(long x) {
>     long y = x*x;
>     long *p = &x;
>     long n = 2*x;
>     do {
>         x += y;
>         (*p)++;
>         n--;
>     } while (n > 0);
>     return x;
> }
> ```
> 
> GCC generates the following assembly code:
> ```Assembly
>     # long dw_loop(long x)
>     # x initially in %rdi
> dw_loop:
>     movq    %rdi, %rax
>     movq    %rdi, %rcx
>     imulq   %rdi, %rcx
>     leaq    (%rdi,%rdi), %rdx
> .L2:
>     leaq    1(%rcx,%rax), %rax
>     subq    $1, %rdx
>     testq   %rdx, %rdx
>     jg      .L2
>     rep; ret
> ```
 
> A. Which registers are used to hold program values `x`, `y`, and `n`?

Being the first and only argument, we can check that `%rdi` holds `x`.

The instructions:
```
movq    %rdi, %rcx
imulq   %rdi, %rcx
```
Show that `%rcx` holds value `x^2`, which agrees with the value of `y`.

Finally, the instruction:
```
leaq  (%rdi,%rdi), %rdx
```
Indicates that `%rdx` holds `2*x`, which corresponds with `n`.
 
> B. How has the compiler eliminated the need for pointer variable `p` and the
> pointer dereferencing implied by the expression `(*p)++`?

It understood that the only role the pointer played was incrementing `x` by 1,
so every time x was incremented by y, as the loop body required, it did both
increments at once `x = y + 1`.
This can be seeen on the instruction `leaq    1(%rcx,%rax), %rax`.
 
> C. Add annotations to the assembly code describing the operation of the program,
> similar to those shown in Figure 3.19(c).

```Assembly
dw_loop:
    movq    %rdi, %rax          # result = x
    movq    %rdi, %rcx          # y = x
    imulq   %rdi, %rcx          # y = y * x (y = x*x)
    leaq    (%rdi,%rdi), %rdx   # n = x + x
.L2:
    leaq    1(%rcx,%rax), %rax  # x += y + 1
    subq    $1, %rdx            # n--
    testq   %rdx, %rdx          # Test n & n
    jg      .L2                 # If n > 0, goto .L2
    rep; ret                    # return
```
